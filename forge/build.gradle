plugins {
	id "com.github.johnrengelman.shadow"
}

loom {
	forge {
		mixinConfig "mtrsteamloco.mixins.json"
	}
	mixin {
		defaultRefmapName = "common-common-refmap.json"
	}
}

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
}

dependencies {
	forge "net.minecraftforge:forge:${rootProject.forge_version}"
	modApi "${rootProject.architectury_id}:architectury-forge:${rootProject.architectury_version}"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

shadowJar {
	exclude "fabric.mod.json"
	exclude "*.bmp"

	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier null
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

task moveForgeBuilds() {
	doLast {
		delete fileTree("../build").matching {
			include "**/MtrSteamLoco-forge*"
		}
		copy {
			from "build/libs"
			into "../build"
			exclude "*dev-shadow.jar"
			rename "(.+)", "MtrSteamLoco-\$1"
		}
	}
}

afterEvaluate {
	for (def task in it.tasks) {
		if (task == tasks.build) {
			task.finalizedBy tasks.moveForgeBuilds
			if (System.env.GITHUB_ACTIONS) {
				task.dependsOn rootProject.tasks.setupLibrary
			}
		}
	}
}
